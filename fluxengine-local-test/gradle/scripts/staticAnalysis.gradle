/*
  Setup tasks for static analysis.
 */

apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

findbugs {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  reportsDir = file("${project.buildDir}/reports/findbugs")
  excludeFilter = new File("${project.projectDir}/config/findbugs-exlude-filter.xml")
  effort = 'max'
}

tasks.withType(FindBugs) {
  reports {
    html {
      enabled = false
      destination = file("${findbugs.reportsDir}/findbugs.html")
    }
    xml.enabled = !html.enabled
  }
}

checkstyle {
  toolVersion = '8.7'
  ignoreFailures = true
  configFile = file("${project.projectDir}/config/checkstyle.xml")
  checkstyleMain.source = "$projectDir/src/main"
  checkstyleTest.enabled = false
}

jacoco {
  toolVersion = '0.7.7.201606060606'
  reportsDir = file("${project.buildDir}/reports/customJacocoReportDir")
}

jacocoTestReport {
  reports {
    xml.enabled false
    csv.enabled false
    html.destination file("${project.buildDir}/reports/jacocoHtml")
  }
}

sonarqube {
  properties {
    property "sonar.projectName", project.properties['SONAR_KEY']
    property "sonar.projectKey", project.properties['SONAR_KEY']
    property "sonar.login", project.properties['SONAR_LOGIN']
    property "sonar.password", project.properties['SONAR_PASSWORD']
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
  }
}
